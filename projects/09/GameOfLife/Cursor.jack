class Cursor {

  field int row;
  field int col;
  field int rowCount;
  field int colCount;

  constructor Cursor new(int rc, int cc) {
    let row = 0;
    let col = 0;

    let rowCount = rc;
    let colCount = cc;

    return this;
  }

  method void move(int direction) {
    do Screen.setColor(false);
    do Screen.drawRectangle(col * 16, row * 16, col * 16 + 15, row * 16 + 15);

    if (direction = 0) { // move left
        if((col - 1) > -1) { let col = col - 1; }
    } else {
      if (direction = 1) { // move up
          if((row - 1) > -1) { let row = row - 1; }
      } else {
        if (direction = 2) { // move right
            if((col + 1) < colCount) { let col = col + 1; }
        } else { // move down
            if((row + 1) < rowCount) { let row = row + 1; }
        }
      }
    }

    do draw();

    return;
  }

  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(col * 16, row * 16, col * 16 + 15, row * 16 + 15);
    return;
  }

  method int getRow() {
    return row;
  }

  method int getCol() {
    return col;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

}
