class GameOfLifeEngine {

  field Cursor cursor;
  field Grid grid;
  field int rowCount;
  field int colCount;

  constructor GameOfLifeEngine new() {
    let rowCount = 16;
    let colCount = 32;
    let cursor = Cursor.new(rowCount, colCount);
    let grid = Grid.new(rowCount, colCount);
    return this;
  }

  method void run() {
    var char key;
    var char previousKey;
    var boolean inSimulation;
    var Grid newGrid;

    var int curRow;
    var int curCol;
    var boolean exit;
    let exit = false;
    let inSimulation = false;

    do cursor.move(0, grid);

    while (~exit) {
      while ((key = 0) & (~exit)) {
        do Sys.wait(50);
        let key = Keyboard.keyPressed();
      }

      if(key = 81) { let exit = true; } // exit

      while ((~(key = 0)) & (~exit) & ~(inSimulation)) {
        if((key > 129) & (key < 134)) {
          do cursor.move(key - 130, grid);
          do Sys.wait(60);
        } else {
          if(key = 129 & ~(previousKey = 129)) {
            let curRow = cursor.getRow();
            let curCol = cursor.getCol();
            do grid.toggle(curRow, curCol);
            do cursor.draw();
            do Sys.wait(30);
          } else {
            if(key = 67) {
              let inSimulation = true;
            }
          }
        }
        let previousKey = key;
        let key = Keyboard.keyPressed();
      }

      while(inSimulation) {
        let newGrid = grid.tick();
        do grid.dispose();
        let grid = newGrid;

        do Sys.wait(50);
        let inSimulation = false;
      }

      let previousKey = key;
    }

    do cursor.dispose();
    do grid.dispose();
    return;
  }

  method void dispose() {
    do cursor.dispose();
    return;
  }

}
